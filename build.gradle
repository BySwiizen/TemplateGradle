plugins {
    id 'java'
    // https://github.com/GradleUp/shadow
    id 'com.gradleup.shadow' version '8.3.9'
    id 'maven-publish'
}

compileJava {
    sourceCompatibility = '21'
    targetCompatibility = '21'
    options.encoding = 'UTF-8'
}


repositories {
    mavenLocal()
    mavenCentral()

    // Sonatype
    maven { url = 'https://oss.sonatype.org/content/groups/public' }
    // JitPack
    maven { url = 'https://jitpack.io' }
    // SpigotMC
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    // FoliaLib
    maven { url = 'https://repo.tcoded.com/releases' }
}

dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly 'org.jetbrains:annotations:26.0.2-1'
    // https://github.com/google/gson
    implementation 'com.google.code.gson:gson:2.10'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.16.0'
    // https://hub.spigotmc.org/stash/projects/SPIGOT/repos/spigot
    compileOnly 'org.spigotmc:spigot-api:1.21.8-R0.1-SNAPSHOT'
    // https://github.com/TechnicallyCoded/FoliaLib
    implementation 'com.tcoded:FoliaLib:0.5.1'
    // https://github.com/dejvokep/boosted-yaml
    implementation 'dev.dejvokep:boosted-yaml:1.3.7'
    // https://github.com/KyoriPowered/adventure
    implementation 'net.kyori:adventure-api:4.22.0'
    implementation 'net.kyori:adventure-text-minimessage:4.22.0'
    implementation 'net.kyori:adventure-text-serializer-legacy:4.22.0'
    // https://github.com/KyoriPowered/adventure-platform
    implementation 'net.kyori:adventure-platform-bukkit:4.4.1'
    // https://github.com/Revxrsal/Lamp
   implementation 'io.github.Revxrsal.Lamp:common:3.3.6'
   implementation 'io.github.Revxrsal.Lamp:bukkit:3.3.6'
    // https://github.com/Bastian/bstats-metrics
    implementation 'org.bstats:bstats-bukkit:3.1.0'
}


shadowJar {
    exclude 'META-INF/**', 'LICENSE'
    relocate 'org.jetbrains.annotations', 'fr.byswiizen.templategradle.libs.annotations'
    relocate 'com.google.gson', 'fr.byswiizen.templategradle.libs.gson'
    relocate 'org.apache.commons', 'fr.byswiizen.templategradle.libs.commonslang3.'
    relocate 'com.tcoded.folialib', 'fr.byswiizen.templategradle.libs.folialib'
    relocate 'dev.dejvokep.boostedyaml', 'fr.byswiizen.templategradle.libs.boostedyaml'
    relocate 'org.bstats', 'fr.byswiizen.templategradle.libs.metrics'
    archiveFileName = project.name + "-" + project.version + ".jar"
}

build {
    dependsOn(shadowJar)
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.groupid
            artifactId project.name
            version project.version
            artifact shadowJar
        }
    }
}


processResources {
    filesMatching("plugin.yml") {
        expand(
            name: project.property("name"),
            groupid: project.property("groupid"),
            artifactid: project.property("artifactid"),
            description: project.property("description"),
            author: project.property("author"),
            version: project.property("version"),
            apiversion: project.property("apiversion"),
        )
    }
}